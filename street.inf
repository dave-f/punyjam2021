! street
! 
! River [Boat] | Street 1 | Street 2 | Graveyard

Object OutsidePub "Outside the Pub"
       with description "Standing just outside the pub you notice the street is eerily deserted.  A lamppost stands proudly outside the pub serving as a sort of beacon, but otherwise takes your eye.  The street runs east to west and south leads back into the pub.",
            after [;
                  Go: if (random(100) <= 10)
                      {
                          if ((Coat hasnt worn) || (Hat hasnt worn))
                          {
                              print "A passer-by walks past just as you are on the street.  He goes into an immediate panic, shouting ~It's not natural!~ and beats you to death with his walking stick.";
                              deadflag=true;
                              return 1;
                          }
                          else
                          {
                              print "A passer-by walks past, glancing over briefly before shaking his head and walking off.^^";
                              return 0;
                          }
                      }
            ],
       e_to PubEast,
       w_to PubWest,
       s_to PubDoor,
       has light;

Object -> LampPost
       with name 'lamp' 'post',
            description "The lamppost is made of decorative iron with a round lamp at the top.  It provides lighting for people who may be a little unsure on their feet after coming out the pub.",
       has scenery;

Object PubWest "River bank"
       with description "The road ends abruptly here to form a small river bank.  The river itself looks calm and runs gently south, however the sunlight is particularly dazzling off the water.",
       e_to OutsidePub,
       has light;

Object  -> boat "wooden boat"
 with   name 'wooden' 'boat',
        initial "A small wooden boat bobs about on the water.",
        description "Presumably once used for recreational purposes, this boat now looks a little worse for wear, although it remains afloat.",
        after [;
         Enter:
            print "You step cautiously into the boat.";

            if (~~(Sunglasses has worn))
            {
                print " As you do so, you are dazzled by the sunlight out on the water, and lose your footing, causing you to fall into the water, which as you are all too aware will prove deadly.";
                deadflag=true;
                rtrue;
            }

            score++;
            !new_line;
            deadflag=2; ! Currently won, but this isn't the end
            rtrue;
        ],
#Iftrue 0;
        before [;
         Go:
            if (selected_direction==s_to && real_location==boating_lake)
            {
                return 1;
            }
            else if (selected_direction==n_to && real_location==island_tip)
            {
                return 1;
            }
            else
            {
                return 0;
            }
        ],
#EndIf;
 has    static enterable container open;

Object PubEast "Street"
       with name 'steps' 'pillar' 'door',
            description "The rather quiet street continues here, where it stops in front of a huge graveyard, fronted by a large iron gate.  Steps lead north into the Police Station, east leads into the Graveyard and west leads back to the pub.",
       n_to InsideStation,
       e_to GraveyardGate,
       w_to OutsidePub,
       has light;

Object -> PoliceStation
       with name 'police' 'station',
             description "The Station is a rather large building, with steps leading north into it.  Concrete pillars line either side of the door and above those it reads POLICE STATION.",
       has scenery;

